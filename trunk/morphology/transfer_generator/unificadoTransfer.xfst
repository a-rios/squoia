set char-encoding UTF-8

       # Alle Tags und Worttrenner auf Wortformenebene löschen!
define Cleanup [ [ "[" ] ?* "]" @> 0 ] ;


#Vowels and Consonants
define Voc [a|i|u];
define Cons [b|c|d|f|g|h|j|k|l|m|n|ñ|p|q|r|s|t|v|w|y|z|'|’];

# symbols that may ocurr in a root (note that the root might be Spanish or something else, if it couldn't be translated!)
define VocRoot 	[a|e|i|o|u|á|é|í|ó|ú|ü];
define ConsRoot [b|c|d|f|g|h|j|k|l|m|n|ñ|p|q|r|s|t|v|w|x|y|z|'|’];
define UPPER 	[A|B|C|D|E|F|G|H|I|J|K|L|M|N|Ñ|O|P|Q|R|S|T|U|V|W|X|Y|Z|Á|É|Í|Ó|Ú|Ü];

#######################################################################
#######################################################################

       #############################
       #                           #
       # PHONETISCHE RESTRIKTIONEN #
       #                           #
       #############################

       #----------------------#
       # Ambivalente Suffixe: #
       #----------------------#

       #  -mi nach Konsonant, -m nach Vokal
       define MI [ "+DirE" -> {m} || Voc _  ];
       define MI2 [ "+DirE" -> {mi} || Cons _  ];
       
       #  -si nach Konsonant, -s nach Vokal
       define SI [ "+IndE" -> {s} || Voc _  ];
       define SI2 [ "+IndE" -> {si} || Cons _  ];

       # -cha nach Konsonant, -ch nach Vokal
       define CHA [ "+Asmp" -> {ch} || Voc _  ];
       define CHA2 [ "+Asmp" -> {cha} || Cons _  ];

       # -iki -> -miki,-siki,-chiki falls mit Evidentialsuffixen, sonst -riki
       define IKI [ "+Res" -> {iki} || ["+DirE"|"+IndE"|"+Asmp"] _  ];
       define IKI2 [ "+Res" -> {riki} || ~$["+DirE"|"+IndE"|"+Asmp"] _  ];

define ASphon [IKI .o. IKI2 .o. MI .o. MI2 .o. SI .o. SI2 .o. CHA .o. CHA2];
       
    #################################################

       #-------------------------#
       # Verbalisierende Suffixe #
       #-------------------------#    

       #------------------------------------------#
       # -kacha nach i, -ykacha sonst (Simulativ) #
       #------------------------------------------#
           define SML ["+Sml" -> {kacha} || i _ ];
           define SML2 ["+Sml" -> {ykacha} || ~${i} _ ];

define NzuV [SML .o. SML2];


   ##################################################
   
       #--------------------------#
       # Verbale Derivation (v-v) #
       #--------------------------#
       
       #-----------------------------------------------------#
       # -kacha/-ykacha , selbe Regel wie oben (Interruptiv) #
       #-----------------------------------------------------#
           define KACHA ["+Intrup" -> {kacha} || i _ ];
           define KACHA2 ["+Intrup" -> {ykacha} || ~$[i] _ ];


       #-------------------------------------------------------#
       # -yku wird zu -yka vor -mu, -pu, -ysi, -chi, -ri, -cha #
       # und -na. -mu hat selbst eine variable Form,           #
       # und kann daher auch als +Cis bzw. -mpu erscheinen #
       #-------------------------------------------------------#
           define trigger [{mu}|{mpu}|"+Cis"|{pu}|{ysi}|{chi}|{ri}|{cha}|{na}|"+Iprs"];
           define YKU ["+Aff" -> {yka} || _ trigger ];
           define YKU2 ["+Aff" -> {yku} || _ ~$[trigger]];
     
       #----------------------------------------------------#
       # -rqu wird zu -rqa vor -mu, -pu, -ysi, -ri und -chi #
       #----------------------------------------------------#
           define trigger2 [{mu}|{mpu}|"+Cis"|{pu}|{ysi}|{chi}|{ri}|"+Iprs"];
           define RQU ["+Rptn" -> {rqa} || _ trigger2 ];
           define RQU2 ["+Rptn" -> {rqu} || _ ~$[trigger2]];

       #------------------------#
       # -mu wird zu -m vor -pu #
       #------------------------#
           define MU ["+Cis" -> m || _ [{pu}|"+Iprs"]];
           define MU2 ["+Cis" -> {mu} || _ ~$[[{pu}|"+Iprs"]]];

       #-------------------------------------------#
       # -ku wird zu -ka vor -chi und  -mu         #
       #-------------------------------------------#
           define trigger3 [{chi}|{mu}|{mpu}];
           define KU ["+Rflx" -> {ka} || _ trigger3, _ "+Cis"];
           define KU2 ["+Rflx" -> {ku} || _ ~$[trigger3]];

       #-------------------------------------------#
       # -pu wird zu -pa vor -ku und  -mu          #
       #-------------------------------------------#
           define trigger4 [{ku}|{mu}|"+Rflx"|"+Cis"];
           define PU ["+Iprs" -> {pa} || _ trigger4];
           define PU2 ["+Iprs" -> {pu} || _ ~$[trigger4]];

define PHON [KACHA .o. KACHA2 .o. YKU .o. YKU2 .o. RQU .o. RQU2 .o. KU .o. KU2 .o. MU .o. MU2 .o. PU .o. PU2];


   ###################################################

       #-----------------#
       # Verbale Flexion #
       #-----------------#     

       #-----------------------------------------------------------------------#
       # spezielle Past-Formen der 3.Person                                    #
       # mit -rqa und -sqa -> 3.Person ohne "n" -rqan -> -rqa/ -rqanku ->rqaku #
       #-----------------------------------------------------------------------#
           define PST [{rqa}|{sqa}];
           define 3SGPST [ "+3.Sg.Subj" -> "" || PST _ ];
           define 3PLPST [ "+3.Pl.Subj" -> {ku} || PST _ ];
           define PAST [3SGPST .o. 3PLPST];

       #-------------------------#
       # Portmanteau Futurformen #
       # 1.Sg -> -waq            #
       # 2.Sg -> -nki            #
       # 3.Sg -> -nqa            #
       # 1.Pl.incl -> -sun       #
       # 1.Pl.excl -> -saqku     #
       # 2.Pl -> nkichis         #
       # 3.Pl -> nqaku           #
       #-------------------------#
           define FUT ["+Fut"];
           define 1SGFUT ["+1.Sg.Subj" -> {saq} || FUT _];
           define 3SGFUT ["+3.Sg.Subj" -> {nqa} || FUT _];
           define 1PLincFUT ["+1.Pl.Incl.Subj" -> {sun} || FUT _];
           define 1PLexcFUT ["+1.Pl.Excl.Subj" -> {saqku} || FUT _];
           define 3PLFUT ["+3.Pl.Subj" -> {nqaku} || FUT _];
           define FUT1 ["+Fut" -> ""];
           define FUTUR [1SGFUT .o. 3SGFUT .o. 1PLincFUT .o. 1PLexcFUT .o. 3PLFUT ];

       #--------------------------------------------#
       # Portmanteau Potentialformen:               #
       # 1.Sg + Pot -> y-man (nicht -ni-man)        #
       # 1.Pl.Excl + Pot -> y-man-ku (nicht ykuman) #
       # 3.Pl + Pot -> nmanku (nicht -nku-man)      #
       #--------------------------------------------#
           define POT ["+Pot"];
           define 1SGPOT ["+1.Sg.Subj" -> {y} || _ POT ];
           define 1PLexcPOT ["+1.Pl.Excl.Subj" -> {ymanku} || _ POT ];
           define 3PLPOT ["+3.Pl.Subj" -> {nmanku} || _ POT ]; 
           define POT2 [ "+Pot" -> {man} || ~$[{nmanku}|{ymanku}] _ ];
           define POT3 [ "+Pot" -> "" || [{nmanku}|{ymanku}] _ ];
           define POTENTIAL [1SGPOT .o. 1PLexcPOT .o. 3PLPOT];

       #------------------------------#
       # Portmanteau Imperativformen: #
       # 2.Sg -> -y                   #
       # 3.Sg -> -chun                #
       # 1.Pl.Incl -> -sun            #
       # 2.Pl -> -ychis               #
       # 3.Pl -> -chunku              #
       #------------------------------#
           define IMP ["+Imp"];
           define 2SGIMP ["+2.Sg.Subj" -> y || _ IMP];
           define 3SGIMP ["+3.Sg.Subj" -> {chun} || _ IMP];
           define 1PLincIMP ["+1.Pl.Incl.Subj" -> {sun} || _ IMP];
           define 2PLIMP ["+2.Pl.Subj" -> {ychik} || _ IMP];
           define 3PLIMP ["+3.Pl.Subj" -> {chunku} || _ IMP];
           define IMP1 ["+Imp" -> ""];
           define IMPERATIV [2SGIMP .o. 3SGIMP .o. 1PLincIMP .o. 2PLIMP .o. 3PLIMP];

define TEMPMOD [PAST .o. POTENTIAL .o. FUTUR .o. IMPERATIV];
define TEMPMODdefault [POT3 .o. POT2 .o. FUT1 .o. IMP1];

       #-----------------------------------------#
       # Portmanteausuffixe mit 2 Partizipanten: #
       #-----------------------------------------#
           define 1SGOBJ ["+1.Sg.Obj"];
           define 1SGSUB ["+1.Sg.Subj"];
           define 2SGOBJ ["+2.Sg.Obj"];
           define 2SGSUB ["+2.Sg.Subj"];
           define 1PLexcOBJ ["+1.Pl.Excl.Obj"];
           define 1PLexcSUB ["+1.Pl.Excl.Subj"];
           define 1PLincOBJ ["+1.Pl.Incl.Obj"];
           define 3SG ["+3.Sg.Subj"];
           define 3PL ["+3.Pl.Subj"];
       #--------------------------------------#
       # 1.Sg > 2.Sg -> -yki                  #
       # 1.Sg > 2.Sg & Fut -> -sqayki         #
       #--------------------------------------#
           define 1SGon2SG ["+2.Sg.Obj" -> "++" || _ $[1SGSUB]] .o. ["+Fut" -> {sqa}, "+1.Sg.Subj" -> {yki} || $["++"] _] .o. ["++" -> "" ];

       #--------------------------------------#
       # 1.Sg > 2.Pl -> -ykichis              #
       # 1.Sg > 2.Pl & Fut-> -sqaykichis      #
       #--------------------------------------#
           define 1SGon2PL ["+2.Pl.Obj" -> "++" || _  $[1SGSUB]] .o. ["+Fut" -> {sqa} ,"+1.Sg.Subj" -> {ykichik} || $["++"] _ ] .o. ["++" -> ""];

       #--------------------------------------#
       # 1.Pl.Excl > 2.Sg  -> -ykiku          #
       # 1.Pl.Excl > 2.Sg & Fut -> -sqaykiku  #
       #--------------------------------------#
           define 1PLon2SG ["+2.Sg.Obj" -> "++", "+2.Pl.Obj" -> "++" || _  $[1PLexcSUB]] .o. ["+Fut" -> {sqa},"+1.Pl.Excl.Subj" -> {ykiku} || $["++"] _ ] .o. ["++" -> ""];
   define 1on2 [1SGon2SG .o. 1SGon2PL .o. 1PLon2SG];

       #--------------------------------------#
       # 2.Sg > 1.Pl.Excl -> -wankiku         #
       # 2.Sg > 1.Pl.Excl & Imp -> -wayku     #
       #--------------------------------------#
           define 2SGon1PLexcIMP ["+2.Sg.Subj" -> {yku} || $["+1.Pl.Excl.Obj"] _  $["+Imp"]];
           define 2SGon1PLexc ["+2.Sg.Subj" -> {nkiku} || $["+1.Pl.Excl.Obj"] _  ~$["+Imp"]];
   define 2on1 [2SGon1PLexcIMP .o. 2SGon1PLexc];

       #--------------------------------------#
       # 3. > 2.Sg -> -sunki                  #
       # 3. > 2.Pl -> -sunkichis              #
       #--------------------------------------#
           define 3on2SG ["+3.Sg.Subj" -> {nki} , "+3.Pl.Subj" -> {nki} || $["+2.Sg.Obj"] _ ];
           define 3on2PL ["+3.Sg.Subj" -> {nkichik} , "+3.Pl.Subj" -> {nkichik} || $["+2.Pl.Obj"] _ ];
   define 3on2 [3on2SG .o. 3on2PL];

       #---------------------------------------------------#
       # 3. > 1.Pl.Incl & Fut -> -wasun                    #
       # 3 > 1.Pl.Incl -> -wanchik                         #
       # 3.Sg > 1.Pl.Excl -> -wanku (nicht -wan)           #
       # 3.Sg > 1.Pl.Excl & Fut -> -wanqaku (nicht -wanqa) #
       # 3.Pl > 1.Sg -> -wan (nicht -wanku)                #
       # 3.Pl > 1.Sg & Fut -> -wanqa (nicht -wanqaku)      #
       #---------------------------------------------------#
           define 3on1PLincFUT["+3.Sg.Subj" -> {sun} , "+3.Pl.Subj" -> {sun} || $[1PLincOBJ] & $[FUT] _ ];
           define 3on1PLinc["+3.Sg.Subj" -> {nchik} , "+3.Pl.Subj" -> {nchik} || $[1PLincOBJ] & ~$[FUT] _ ];
           define 3SGon1PLexcFUT["+3.Sg.Subj" -> {nqaku} || $[1PLexcOBJ] & $[FUT] _ ];
           define 3SGon1PLexc["+3.Sg.Subj" -> {nku} || $[1PLexcOBJ] & ~$[FUT] _ ];
           define 3PLon1SGFUT["+3.Pl.Subj" -> {nqa} || $[1SGOBJ] & $[FUT] _ ];
           define 3PLon1SG["+3.Pl.Subj" -> {n} || $[1SGOBJ] & ~$[FUT] _ ];
   define 3on1 [3on1PLincFUT.o. 3on1PLinc .o. 3SGon1PLexcFUT .o. 3SGon1PLexc .o. 3PLon1SGFUT .o. 3PLon1SG];   
define SubjObj [1on2 .o. 2on1 .o. 3on2 .o. 3on1];

       #--------------------------#
       # Default Personalsuffixe: #
       #--------------------------#
           define 1SGPERS ["+1.Sg.Subj" -> {ni} ||~$[FUT] _ ~$[POT|IMP] ];
           define 2SGPERS ["+2.Sg.Subj" -> {nki} || _ ~$[IMP] ];
           define 3SGPERS [ "+3.Sg.Subj" -> {n} || ~$[PST|FUT] _ ];
           define 1PLincPERS [ "+1.Pl.Incl.Subj" -> {nchik} || ~$[FUT] _ ];
           define 1PLexcPERS [ "+1.Pl.Excl.Subj" -> {yku} || ~$[FUT] _ ];
           define 2PLPERS ["+2.Pl.Subj" -> {nkichik} || _ ~$[IMP] ];
           define 3PLPERS [ "+3.Pl.Subj" -> {nku} || ~$[PST|FUT] _ ~$[POT]];

           define 1SGPERSobj ["+1.Sg.Obj" -> {wa}];
           define 2SGPERSobj ["+2.Sg.Obj" -> {su}];
           define 1PLincPERSobj [ "+1.Pl.Incl.Obj" -> {wa}];
           define 1PLexcPERSobj [ "+1.Pl.Excl.Obj" -> {wa}];
           define 2PLPERSobj ["+2.Pl.Obj" -> {su} ];

define PERS [1SGPERS .o. 2SGPERS .o. 3SGPERS .o. 1PLincPERS .o. 1PLexcPERS .o. 2PLPERS .o. 3PLPERS .o. 1SGPERSobj .o. 2SGPERSobj .o. 1PLincPERSobj .o. 1PLexcPERSobj .o. 2PLPERSobj];

define VDERIV [PHON .o. SubjObj .o. TEMPMOD   .o. PERS .o. TEMPMODdefault];

   ###################################################

       #------------------#
       # Nominale Flexion #
       #  und Derivation  #
       #------------------#

       #-------------------------#     
       # Genitiv: -q nach Vokal, # 
       #  -pa nach Konsonant     #
       #-------------------------#
           define GEN1 ["+Gen" -> {pa} || Cons _];
           define GEN2 ["+Gen" -> {p} || Voc _];
       define GEN [GEN1 .o. GEN2];

       #---------------------------------------#
       # Epenthese von -ni bei Possessiv- und  #
       # davon abgeleiteten Suffixen           #
       # (betreffende Suffixe im Lexikon mit % #
       #---------------------------------------#
           define POSS [{y}|{yki}|{n}|{nchik}|{yku}|{ykichik}|{nku}|{ntin}|{nta}|{yuq}];
           define NI1 [ "%" -> {ni} || Cons _ ];
           define NI2 [ "%" -> "" || Voc _ ];
       define NI [NI1 .o. NI2];

       #-----------------------------------------#
       # Possessivsuffix auf -ku + Plural -kuna: #
       # -ku -kuna -> kuna, also z .B. nicht     #
       # wasi -yku -kuna, sondern wasiykuna      #
       # (kuna im Lexikon als "=na")             #
       #-----------------------------------------#
           define KUNA1 [ {=na} -> {na} || {%yku} _ , {%nku} _  ];
           define KUNA2 [ {=na} -> {kuna} || ~$[{%yku}|{nku}] _ ];
       define KUNA [KUNA1 .o. KUNA2];

define NDERIV [GEN .o. KUNA .o. NI];

######################################################################
######################################################################

       #######################
       #                     #
       # Ambivalente Suffixe #
       #                     #
       #######################

define AS=1  @re"as-1.regex";
define AS=2  @re"as-2.regex";
define AS=3  @re"as-3.regex";
define AS=4  @re"as-4.regex";
define AS=5  @re"as-5.regex";
define AS=6  @re"as-6.regex";
define AS=7  @re"as-7.regex";

define HonAff @re"lim.regex" ;

define AS [(HonAff) (AS=1) (HonAff) (AS=2) (AS=3)^{0,2} (HonAff) (AS=4)(HonAff) (AS=5) (AS=6) (AS=7)];


######################################################################
######################################################################

       ##########
       #        #
       # Roots  #
       #        #
       ##########
       
       
       define Root [ (UPPER) [VocRoot|ConsRoot|%_]+ (":"):0];

######################################################################
######################################################################

       ##########
       #        #
       # Verben #
       #        #
       ##########


define V=V=1 @re"v-v-1.regex" ;
define V=V=2 @re"v-v-2.regex" ;
define V=V=3 @re"v-v-3.regex" ;
define V=V=4 @re"v-v-4.regex" ;
define V=V=5 @re"v-v-5.regex" ;
define V=V=6 @re"v-v-6.regex" ;
define V=V=7 @re"v-v-7.regex" ;


define VerbSuffixes [V=V=1^{0,2} V=V=2^{0,3} (V=V=1) (V=V=3) (V=V=4)(HonAff) (V=V=5) (HonAff) V=V=6 (V=V=7) (AS)];


#########################################################################
#########################################################################

       #########
       #       #
       # Nomen #
       #       #
       #########

       #-----------------------------------------#
       # nominale Wurzeln:                       #
       # NRootCMP: keine Compound Nouns, sollen  #
       # getrennt geschrieben werden, z.B.       #
       # llaqta --> "llaqta masi                 #
       #-----------------------------------------#

define N=N=1 @re"n-n-1.regex" ;
define N=N=2 @re"n-n-2.regex" ;
define N=N=3 @re"n-n-3.regex" ;
define N=N=4 @re"n-n-4.regex" ;
define N=N=5 @re"n-n-5.regex" ;
define N=N=6 @re"n-n-6.regex" ;
define N=N=7 @re"n-n-7.regex" ;

define NounSuffixes [(N=N=1) (HonAff) (N=N=2)^{0,2} (N=N=3) (N=N=4) (N=N=5)^{0,2} (HonAff) N=N=6^{0,3} N=N=7^{0,2} (HonAff) (AS)];


#########################################################################
#########################################################################

       ###############################
       #                             # 
       # DERVIERTE VERBEN  UND NOMEN #
       #                             #
       ###############################

       #----------------------#
       # Derivationssuffixe : #
       # N -> V = NtoV        #
       # V -> N = VtoN        #
       #----------------------#
       define NtoV @re"n-v.regex" ;
       define VtoN @re"v-n.regex" ;
       define VtoNfinal @re"v-n-final.regex" ; #mit diesen Suffixen nominalisierte Verben können nicht wieder verbalisiert werden
       define Relational "+Rel" : {n};

       #------------------------#
       # nominalisierte Verben: #
       # VRoot + VtoN = NDeriv1 #
       # "taki-qti", "taki-y"   #
       #------------------------#
       define NDeriv1 [Root V=V=1^{0,3} V=V=2^{0,3} (V=V=3) (V=V=4) VtoN];
       define NDeriv1final [Root V=V=1^{0,3} V=V=2^{0,3} (V=V=3) (V=V=4) VtoNfinal];

       #----------------------------#
       # verbalisierte Nomen:       #
       # NRoot + NtoV = VDeriv1     #
       # "wasi-n-cha-", "hatun-ya-" #
       # NzuV: -ykacha/kacha als    #
       # verbalisierndes Suffix     #
       #----------------------------#
       define VDeriv1 [Root (Relational) (HonAff) NtoV] .o. NzuV;

       #--------------------------------#
       # nominalisierte Verben Nr.2:    #
       # NRoot + NtoV + VtoN = NDeriv2  #
       # "wasi-n-cha-y", "hatun-ya-sqa" #
       #--------------------------------#
       define NDeriv2 [VDeriv1 V=V=1^{0,2} (V=V=2)(V=V=3) (V=V=4) VtoN];
       define NDeriv2final [VDeriv1 V=V=1^{0,2} (V=V=2) (V=V=3) (V=V=4) VtoNfinal];

       #--------------------------------#
       # verbalisierte Nomen Nr.2:      #
       # VRoot + VtoN + NtoV = VDeriv1  #
       # "yaku-naya-q"                  #
       #--------------------------------#
       define VDeriv2 [NDeriv1 NtoV] .o. NzuV;

       #-------------------------------------#
       # nominalisierte Verben Nr.3:         #
       # VRoot + VtoN + NtoV +VtoN = NDeriv3 #
       # "yuya-y-cha-ku-y"                   #
       #-------------------------------------#
       define NDeriv3 [VDeriv2 V=V=1^{0,2} (V=V=2) VtoN];
       define NDeriv3final [VDeriv2 V=V=1^{0,2} (V=V=2) VtoNfinal];

define NDeriv [NDeriv1|NDeriv1final|NDeriv2|NDeriv2final|NDeriv3|NDeriv3final];
define VDeriv [VDeriv|VDeriv1|VDeriv2];

define Noun [[Root|NDeriv] NounSuffixes ].o. VDERIV .o. NDERIV; 
define Verb [[Root|VDeriv] VerbSuffixes ] .o. VDERIV;

#########################################################################

 
#########################################################################

       #-------------#
       # Satzzeichen #
       #-------------#
       
       define punc [%,|%.|"%"|'|%;|%/|"-"|%!|%?|%+|%(|%)|%[|%]|%:|%&|%"|%\|%^|%~|%`|%°|%§|%¿|%¡|%»|%«|%’|%@|%#|%*|%=|%}|%{|%_|%<|%>];
       


#########################################################################


       #-------------------------------------------------#
       # Zusammengehängt:                                #
       # der Trennstrich der Compound-Nomen              #
       # und den Verben mit inkorporierter Nominalwurzel #
       # muss noch entfernt werden                       #
       #-------------------------------------------------#

define generator1 [Noun|Verb|punc] .o. ASphon .o. ["-" -> "" ] ; 

read regex generator1 .i;

save stack unificadoTransfer.fst
